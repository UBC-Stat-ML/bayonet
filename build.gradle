apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'eclipse'
apply plugin: 'application'


uploadArchives {
  repositories.mavenDeployer {
    repository(url: "file:///${System.properties['user.home']}/artifacts/")
    pom.version = "4.0.12"
    pom.artifactId = "bayonet"
    pom.groupId = "ca.ubc.stat"
  }
}

eclipse { 
  classpath {
    defaultOutputDir = file('bin')
  }
}

repositories {
  mavenCentral()
  jcenter()
  maven {
    url "https://ubc-stat-ml.github.io/artifacts/"
  }
  maven {
    url "https://www.stat.ubc.ca/~bouchard/maven/"
  }
}

dependencies {
  compile group: 'com.google.guava', name: 'guava', version: '18.0'
  testCompile group: 'junit', name: 'junit', version: '4.12'
  compile group: 'junit', name: 'junit', version: '4.12'
  compile group: 'ca.ubc.stat', name: 'briefj', version: '2.4.13'
  compile group: 'ca.ubc.stat', name: 'binc', version: '1.2.1'
  compile 'net.sf.opencsv:opencsv:2.3'
  compile 'com.googlecode.efficient-java-matrix-library:ejml:0.24'
  compile 'commons-io:commons-io:2.4'
  compile 'org.mvel:mvel2:2.1.8.Final'
  compile 'org.apache.commons:commons-math3:3.4.1'
  compile group: 'org.jgrapht', name: 'jgrapht-dist', version: '0.9.0'
  compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.3.2'
  compile 'org.jblas:jblas:1.2.3'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

configurations.archives.with {
    artifacts.remove artifacts.find { it.archiveTask.is distZip }
    artifacts.remove artifacts.find { it.archiveTask.is distTar }
}


if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        //noinspection SpellCheckingInspection
        tasks.withType(Javadoc) {
            // disable the crazy super-strict doclint tool in Java 8
            //noinspection SpellCheckingInspection
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}



mainClassName = 'blang.prototype.Main'
